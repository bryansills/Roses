buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath Libs.gradle_versions_plugin
        classpath Libs.android_gradle_plugin
        classpath Libs.kotlin_gradle_plugin
        classpath Libs.navigation_safe_args_gradle_plugin
        classpath Libs.ktlint_gradle_plugin
        classpath Libs.detekt_gradle_plugin
    }
}

plugins {
    id("com.github.ben-manes.versions") version "0.38.0"
    id("com.autonomousapps.dependency-analysis") version "0.71.0"
}

allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.gitlab.arturbosch.detekt"

    repositories {
        google()
        // AndroidX snapshots. Remove this once Room 2.2.0-alpha03 is released
        maven { url 'https://ci.android.com/builds/submitted/5799541/androidx_snapshot/latest/repository' }
        jcenter()
    }

    afterEvaluate {
        if (project.plugins.hasPlugin("kotlin-kapt")) {
            kapt {
                javacOptions {
                    option("-Xmaxerrs", 500)
                }
            }
        }
        if (project.plugins.hasPlugin("com.android.application") || project.plugins.hasPlugin("com.android.library")) {
            ktlint {
                android = true
            }
        }
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_1_8
        task.targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                    '-XXLanguage:+NewInference',
                    '-progressive',
                    '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                    '-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi',
            ]
        }
    }

    ktlint {
        enableExperimentalRules = true
        ignoreFailures = true
        additionalEditorconfigFile = rootProject.file(".editorconfig")
    }

    detekt {
        config = rootProject.files("config/detekt/detekt.yml")
        baseline = project.file("detekt-baseline.xml")
    }
}
